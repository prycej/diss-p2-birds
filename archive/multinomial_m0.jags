model{
  # Multinomial likelihood
  for (t in 1:(n.occasions-1)){  # rows of the m-array/p-array
    marr[t, ] ~ dmulti(parr[t, ], R[t])
  }

  # Priors
  # Using vectors for phi and p simplies creating the p-array
  for(t in 1:(n.occasions - 1)) {
    phi[t] <- phi0       # Prior for survival
    p[t] <- p0           # Prior for recapture
    }
    phi0 ~ dbeta(alpha_phi,beta_phi)
    p0 ~ dbeta(alpha_p,beta_p)

  # Create the p-array, the cell probabilities of the m-array:
  for (t in 1:(n.occasions-1)){
    # Main diagonal
    parr[t,t] <- phi[t] * p[t]
    # Above main diagonal
    for (j in (t+1):(n.occasions-1)){
      parr[t,j] <- prod(phi[t:j]) * prod(1 - p[t:(j-1)]) * p[j]
    }
    # Below main diagonal
    for (j in 1:(t-1)){
      parr[t,j] <- 0
    }
    # Last column: probability never recaptured
    parr[t,n.occasions] <- 1 - sum(parr[t,1:(n.occasions-1)])
  }
}